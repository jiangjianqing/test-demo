*** Settings ***
Library           HttpLibrary.HTTP
Library           String

*** Keywords ***
Entity Found
    [Arguments]    ${host}    ${site}    ${entity_name}    ${entity_id}
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}/${entity_id}
    GET    ${base_addr}
    ${response_status}=    Get Response Status
    Should Start With    ${response_status}    200
    ${body}=    Get Response Body
    ${test_status}    Set Variable    执行成功
    Log    执行成功
    [Return]    ${body}

Entity Not Found
    [Arguments]    ${host}    ${site}    ${entity_name}    ${entity_id}
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}/${entity_id}
    Next Request May Not Succeed
    GET    ${base_addr}
    ${response_status}=    Get Response Status
    Should Start With    ${response_status}    404
    ${body}=    Get Response Body
    Should Be Valid Json    ${body}
    [Return]    ${body}

Add Entity
    [Arguments]    ${host}    ${site}    ${entity_name}    ${request_body}
    [Documentation]    常规的POST提交方式,要求输入的request_body格式为：
    ...    username=中文测试&password=111111
    [Tags]    deprecated
    Log    该方法不要调用，标记为deprecated
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}
    Set Request Body    ${request_body}
    POST    ${base_addr}
    ${response_status}=    Get Response Status

Entity Duplicated
    [Arguments]    ${host}    ${site}    ${entity_addr}    ${request_body}
    ${result}    Add Entity By Json    ${host}    ${site}    ${entity_addr}    ${request_body}
    ${new_id}    Get Json Value    ${result}    /id
    Run Keyword If    ${new_id}<=${0}    FAIL    "新增user失败"
    ${errormsg}    Run Keyword And Expect Error    *    Add Entity By Json    ${host}    ${site}    ${entity_addr}
    ...    ${request_body}
    Delete Entity    ${host}    ${site}    ${entity_addr}    ${new_id}

Add Entity By Json
    [Arguments]    ${host}    ${site}    ${entity_name}    ${request_body}
    [Documentation]    支持服务器端使用@RequestBody处理输入数据，可以减少代码量,要求request_body的格式为一个json字符串
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}
    Set Request Header    Content-Type    application/json
    Set Request Body    ${request_body}
    POST    ${base_addr}
    ${response_status}=    Get Response Status
    ${response_body}    Get Response Body
    Should Be Valid Json    ${response_body}
    [Return]    ${response_body}

Delete Entity
    [Arguments]    ${host}    ${site}    ${entity_name}    ${entity_id}
    Create Http Context    ${host}
    comment    重要    通过evaluate去除了字符串两边的引号
    ${entity_id}    evaluate    ${entity_id}
    ${base_addr}    Set Variable    /${site}/${entity_name}/${entity_id}
    DELETE    ${base_addr}
    ${response_status}=    Get Response Status
    Response Status Code Should Equal    200
    Log    下一步一定要FAIL才行
    Comment    重要内容    使用Run Keyword And Expect Error    预期该步骤执行失败
    ${error_msg}    Run Keyword And Expect Error    *    Entity Found    ${host}    ${site}    ${entity_name}
    ...    ${entity_id}
    ${find}    Entity Not Found    ${host}    ${site}    ${entity_name}    ${entity_id}

Out Of PageRange
    [Arguments]    ${host}    ${site}    ${entity_name}    ${invalidPageNumber}
    [Documentation]    page的要求是>0 and <maxPage
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}?page=${invalidPageNumber}
    Next Request May Not Succeed
    Get    ${base_addr}
    ${response_status}=    Get Response Status
    Should Start With    ${response_status}    416
    ${response_body}    Get Response Body
    ${response_body}    Decode Bytes To String    ${response_body}    UTF-8
    Log    ${response_body}

Default Page
    [Arguments]    ${host}    ${site}    ${entity_name}
    [Documentation]    检测是否提供了默认分页功能
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}
    Get    ${base_addr}
    ${response_status}=    Get Response Status
    Should Start With    ${response_status}    200
    ${response_body}    Get Response Body
    Should Be Valid Json    ${response_body}
    ${currentPage}    Get Json Value    ${response_body}    /currentPage
    ${currentPage}    Convert To Integer    ${currentPage}
    Should Be Equal    ${currentPage}    ${1}

Entity Validate Failed
    [Arguments]    ${host}    ${site}    ${entity_name}    ${request_body}
    [Documentation]    支持服务器端使用@RequestBody处理输入数据，可以减少代码量,要求request_body的格式为一个json字符串
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}
    Set Request Header    Content-Type    application/json
    Set Request Body    ${request_body}
    Next Request May Not Succeed
    POST    ${base_addr}
    ${response_status}=    Get Response Status
    Should Start With    ${response_status}    412
    ${response_body}    Get Response Body
    Should Be Valid Json    ${response_body}
    [Return]    ${response_body}

Modify Entity
    [Arguments]    ${host}    ${site}    ${entity_name}    ${entity_id}    ${request_body}
    [Documentation]    支持服务器端使用@RequestBody处理输入数据，可以减少代码量,要求request_body的格式为一个json字符串
    Create Http Context    ${host}
    ${base_addr}    Set Variable    /${site}/${entity_name}/${entity_id}
    Set Request Header    Content-Type    application/json
    Set Request Body    ${request_body}
    PUT    ${base_addr}
    ${response_status}=    Get Response Status
    ${response_body}    Get Response Body
    Should Be Valid Json    ${response_body}
